#
# Set compile flags for entire src/ directory
#
enable_tnt_compile_flags()

if (NOT TARGET_OS_DARWIN)
    add_subdirectory(plugin)
endif()

include_directories(${LIBEV_INCLUDE_DIR})
include_directories(${LIBEIO_INCLUDE_DIR})
include_directories(${LIBCORO_INCLUDE_DIR})
include_directories(${LIBGOPT_INCLUDE_DIR})

# Require pthread globally if compiling with GCC
if (CMAKE_COMPILER_IS_GNUCC)
    add_compile_flags("C;CXX" "-pthread")
endif()

# Compile src/lua/*.lua files into src/lua/*.lua.c sources
file(MAKE_DIRECTORY ${CMAKE_BINARY_DIR}/src/lua)
set(lua_sources)
lua_source(lua_sources lua/uuid.lua)
lua_source(lua_sources lua/session.lua)
set(bin_sources)
bin_source(bin_sources bootstrap.snap bootstrap.h)

add_custom_target(generate_lua_sources
    WORKING_DIRECTORY ${CMAKE_BINARY_DIR}/src/box
    DEPENDS ${lua_sources})
set_property(DIRECTORY PROPERTY ADDITIONAL_MAKE_CLEAN_FILES ${lua_sources})

#
# Used by modules.
#
set (recompiled_sources
     ${CMAKE_SOURCE_DIR}/src/tarantool.cc
     ${CMAKE_SOURCE_DIR}/src/lua/init.cc
     ${CMAKE_SOURCE_DIR}/src/replication.cc)

set (common_sources
     memory.cc
     fiber.cc
     util.cc
     sio.cc
     evio.cc
     coio.cc
     coeio.cc
     iobuf.cc
     coio_buf.cc
     salloc.cc
     pickle.cc
     coro.cc
     stat.cc
     log_io.cc
     recovery.cc
     admin.cc
     replica.cc
     replica.1.5.cc
     iproto.cc
     iproto_port.cc
     session.cc
     object.cc
     exception.cc
     ipc.cc
     errinj.cc
     errcode.c
     assoc.c
     say.c
     fio.c
     plugin.c
     crc32.c
     tbuf.c
     opts.c
     cpu_feature.c
     lua/fiber.cc
     lua/trigger.cc
     lua/admin.cc
     lua/info.cc
     lua/stat.cc
     lua/errinj.cc
     lua/slab.cc
     lua/plugin.cc
     lua/ipc.cc
     lua/socket.cc
     lua/session.cc
     lua/cjson.cc
     lua/yaml.cc
     lua/msgpack.cc
     lua/utils.c
     ${lua_sources}
     ${bin_sources}
)

if (ENABLE_TRACE)
    set (common_sources ${common_sources} trace.c)
endif()

if (ENABLE_DTRACE_USDT AND ENABLE_DTRACE)
    set (common_sources ${common_sources} lua/usdt.cc)
endif()

set_source_files_compile_flags(${common_sources})
add_library(core STATIC ${common_sources})
add_dependencies(core generate_headers)

set (common_libraries cfg core small salad)

list(APPEND common_libraries
    ${LIBEIO_LIBRARIES}
    ${LIBGOPT_LIBRARIES}
    ${LIBYAML_LIBRARIES}
    ${LUAJIT_LIB}
    misc
)

set (THREAD_LIB pthread)
if (ENABLE_STATIC)
    set (THREAD_LIB -Wl,--whole-archive pthread -Wl,--no-whole-archive)
endif()

set (common_libraries ${common_libraries} ${THREAD_LIB})

if (TARGET_OS_LINUX OR TARGET_OS_DEBIAN_FREEBSD)
    set (common_libraries ${common_libraries} dl)
endif()

if (TARGET_OS_FREEBSD AND NOT TARGET_OS_DEBIAN_FREEBSD)
    find_library (INTL intl)
    if (NOT INTL)
        message(FATAL_ERROR "intl library not found")
    else()
        set (common_libraries ${common_libraries} ${INTL})
    endif()
endif()

if (ENABLE_BACKTRACE AND HAVE_BFD)
    find_package (ZLIB)
    set (common_libraries ${common_libraries} bfd ${ZLIB_LIBRARIES})
    if (NOT TARGET_OS_DARWIN)
        set (common_libraries ${common_libraries} iberty)
    endif()
    if (TARGET_OS_FREEBSD AND NOT TARGET_OS_DEBIAN_FREEBSD)
        set (common_libraries ${common_libraries} iconv)
    endif()
endif()

if (ENABLE_DTRACE)
    if (TARGET_OS_FREEBSD)
        find_library (ELF elf)
        if (NOT ELF)
            message (FATAL_ERROR "Not found libelf library, DTrace depended by one")
        else()
            set (common_libraries ${common_libraries} ${ELF})
        endif()
     endif()
    if (ENABLE_DTRACE_USDT)
        set (common_libraries ${common_libraries} ${LIBUSDT_LIBRARIES})
    endif()
else()
    list(APPEND common_libraries
        ${LIBEV_LIBRARIES}
        ${LIBCORO_LIBRARIES}
        ${LIBCJSON_LIBRARIES}
    )
endif()

set (common_libraries ${common_libraries} PARENT_SCOPE)

add_subdirectory(lib)
# Save CMAKE_XXX_FLAGS from this directory for config.h (used in --version)
set(TARANTOOL_C_FLAGS ${CMAKE_C_FLAGS} PARENT_SCOPE)
set(TARANTOOL_CXX_FLAGS ${CMAKE_CXX_FLAGS} PARENT_SCOPE)

function(tarantool_module mod)
    set (module_sources ${ARGN})
    set(cfg_c_flags "-Wno-unused -Wno-unused-parameter")
    if (CMAKE_COMPILER_IS_CLANG)
        set(cfg_c_flags "${cfg_c_flags} -Wno-semicolon-before-method-body")
    endif()
    set_source_files_properties(
        ${CMAKE_SOURCE_DIR}/cfg/tarantool_${mod}_cfg.c
        PROPERTIES COMPILE_FLAGS ${cfg_c_flags}
        GENERATED True)
    unset(cfg_c_flags)
    if (ENABLE_DTRACE AND NOT TARGET_OS_DARWIN)
        set(dtrace_obj ${DTRACE_O_DIR}/dtrace_${mod}.o)
        set(extern_libs
            ${LIBCJSON_LIBRARIES}
            ${LIBCORO_LIBRARIES}
            ${LIBEV_LIBRARIES}
        )
        add_custom_command(OUTPUT ${dtrace_obj}
            COMMAND ${DTRACE} -G -s ${DTRACE_D_FILE} -o ${dtrace_obj} ${extern_libs}
        )
        set_source_files_properties(
           ${dtrace_obj} ${extern_libs}
            PROPERTIES
            EXTERNAL_OBJECT true
            GENERATED true
        )
        list(APPEND module_sources
            ${dtrace_obj}
            ${extern_libs}
        )
        unset(dtrace_obj)
        unset(extern_libs)
    endif()
    add_executable(tarantool_${mod}
        ${module_sources}
        ${CMAKE_SOURCE_DIR}/cfg/tarantool_${mod}_cfg.c)

    set_source_files_properties(${recompiled_sources}
        PROPERTIES OBJECT_DEPENDS
        ${CMAKE_SOURCE_DIR}/cfg/tarantool_${mod}_cfg.h)

    set_source_files_compile_flags(
        ${recompiled_sources} ${module_sources})

    add_library(lt${mod} STATIC ${recompiled_sources})
    add_dependencies(lt${mod} generate_headers generate_admin_cc build_bundled_libs)

    target_link_libraries(tarantool_${mod} lt${mod} ${common_libraries} -rdynamic)

    if (ENABLE_STATIC)
        set_target_properties(tarantool_${mod} PROPERTIES
            LINK_SEARCH_END_STATIC ON)
        set (module_link_flags "${module_link_flags} -static")
    endif()

    if (module_link_flags)
        set_target_properties(tarantool_${mod} PROPERTIES
            LINK_FLAGS ${module_link_flags})
    endif()
    install (TARGETS tarantool_${mod} DESTINATION bin)
endfunction()

foreach (module ${TARANTOOL_MODULES})
add_subdirectory(${module})
endforeach()
